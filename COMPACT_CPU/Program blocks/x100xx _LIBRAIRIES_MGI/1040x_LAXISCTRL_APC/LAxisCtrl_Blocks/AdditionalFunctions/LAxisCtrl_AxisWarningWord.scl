FUNCTION_BLOCK "LAxisCtrl_AxisWarningWord"
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : 'tech.team.motioncontrol@siemens.com'
FAMILY : AxisCtrl
VERSION : 1.1
   VAR_INPUT 
      axis : DB_ANY;
   END_VAR

   VAR_OUTPUT 
      status { ExternalWritable := 'False'} : Word;
      warningWord { ExternalWritable := 'False'; S7_HiddenAssignment := 'HideIfNoParamAssigned'} : DWord;
      systemWarning { ExternalWritable := 'False'; S7_HiddenAssignment := 'HideIfNoParamAssigned'} : Bool;
      configWarning { ExternalWritable := 'False'; S7_HiddenAssignment := 'HideIfNoParamAssigned'} : Bool;
      userWarning { ExternalWritable := 'False'; S7_HiddenAssignment := 'HideIfNoParamAssigned'} : Bool;
      commandNotAccepted { ExternalWritable := 'False'; S7_HiddenAssignment := 'HideIfNoParamAssigned'} : Bool;
      driveWarning { ExternalWritable := 'False'; S7_HiddenAssignment := 'HideIfNoParamAssigned'} : Bool;
      sensorWarning { ExternalWritable := 'False'; S7_HiddenAssignment := 'HideIfNoParamAssigned'} : Bool;
      dynamicError { ExternalWritable := 'False'; S7_HiddenAssignment := 'HideIfNoParamAssigned'} : Bool;
      communicationWarning { ExternalWritable := 'False'; S7_HiddenAssignment := 'HideIfNoParamAssigned'} : Bool;
      swLimitMin { ExternalWritable := 'False'; S7_HiddenAssignment := 'HideIfNoParamAssigned'} : Bool;
      swLimitMax { ExternalWritable := 'False'; S7_HiddenAssignment := 'HideIfNoParamAssigned'} : Bool;
      homingWarning { ExternalWritable := 'False'; S7_HiddenAssignment := 'HideIfNoParamAssigned'} : Bool;
      followingErrorWarning { ExternalWritable := 'False'; S7_HiddenAssignment := 'HideIfNoParamAssigned'} : Bool;
      positioningWarning { ExternalWritable := 'False'; S7_HiddenAssignment := 'HideIfNoParamAssigned'} : Bool;
      peripheralWarning { ExternalWritable := 'False'; S7_HiddenAssignment := 'HideIfNoParamAssigned'} : Bool;
      synchronousWarning { ExternalWritable := 'False'; S7_HiddenAssignment := 'HideIfNoParamAssigned'} : Bool;
      adaptionWarning { ExternalWritable := 'False'; S7_HiddenAssignment := 'HideIfNoParamAssigned'} : Bool;
   END_VAR

   VAR_TEMP 
      tempWarningWord : DWord;
   END_VAR

   VAR CONSTANT 
      ERROR_INVALID_TECHNOLOGY_OBJECT : Word := 16#8002;
      FINISHED : Word;
   END_VAR


BEGIN
	REGION HEADER
	  //================================================================================
	  // SIEMENS AG
	  // (c)Copyright 2020 All Rights Reserved
	  //--------------------------------------------------------------------------------
	  // Library: LAxisCtrl 
	  // Tested with: S7-1515-2 PN
	  // Engineering: TIA Portal V16
	  // Restrictions: -
	  // Requirements: S7-1500 FW 2.8.1
	  // Functionality: The FB is splitting the warning word of an axis (DB_Any) into bits.
	  // Support: tech.team.motioncontrol@siemens.com
	  // Note: Parameter comments are only available in language 'English (United States)'
	  //--------------------------------------------------------------------------------
	  // Change log table:
	  // Version  Date        Expert in charge  Changes applied
	  // 01.01.00 12.03.2020  RS                First released version
	  //================================================================================
	END_REGION
	
	CASE TypeOfDB(#axis) OF
	  TO_ExternalEncoder:
	    #tempWarningWord := "LAxisCtrl_UnpubWarningWordExtEncoder"(#axis);
	    #status := #FINISHED;
	    
	  TO_SpeedAxis:
	    #tempWarningWord := "LAxisCtrl_UnpubWarningWordSpeedAxis"(#axis);
	    #status := #FINISHED;
	    
	  TO_PositioningAxis:
	    #tempWarningWord := "LAxisCtrl_UnpubWarningWordPosAxis"(#axis);
	    #status := #FINISHED;
	    
	  TO_SynchronousAxis:
	    #tempWarningWord := "LAxisCtrl_UnpubWarningWordSyncAxis"(#axis);
	    #status := #FINISHED;
	    
	  ELSE
	    #tempWarningWord := 16#0000_0000;
	    #status := #ERROR_INVALID_TECHNOLOGY_OBJECT;
	    ENO := FALSE;
	END_CASE;
	
	#warningWord := #tempWarningWord;
	//----------------------------------------------------------------------------------------------
	// Write outputs
	//----------------------------------------------------------------------------------------------
	#systemWarning := #tempWarningWord.%X0;
	#configWarning := #tempWarningWord.%X1;
	#userWarning := #tempWarningWord.%X2;
	#commandNotAccepted := #tempWarningWord.%X3;
	#driveWarning := #tempWarningWord.%X4;
	#sensorWarning := #tempWarningWord.%X5;
	#dynamicError := #tempWarningWord.%X6;
	#communicationWarning := #tempWarningWord.%X7;
	#swLimitMin := #tempWarningWord.%X8;
	#swLimitMax := #tempWarningWord.%X9;
	#homingWarning := #tempWarningWord.%X10;
	#followingErrorWarning := #tempWarningWord.%X11;
	#positioningWarning := #tempWarningWord.%X12;
	#peripheralWarning := #tempWarningWord.%X13;
	#synchronousWarning := #tempWarningWord.%X14;
	#adaptionWarning := #tempWarningWord.%X15;
END_FUNCTION_BLOCK

