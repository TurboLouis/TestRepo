FUNCTION "LConSMC_Linear" : LReal
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      x : LReal;
      x1 : LReal;
      y1 : LReal;
      x2 : LReal;
      y2 : LReal;
   END_VAR

   VAR_TEMP 
      tmpXcalc1 : LReal;
      tmpYcalc1 : LReal;
      tmpXcalc2 : LReal;
      tmpYcalc2 : LReal;
   END_VAR

   VAR CONSTANT 
      "UNIT ID" : DWord := 16#0001_0000;
   END_VAR


BEGIN
	//------------------------------------------------------------------------------------------------------------------------
	//SIEMENS DF FA PMA APC / Mumbai,India | Erlangen, Germany
	//------------------------------------------------------------------------------------------------------------------------
	//name          : Linear
	//functionality : Linear interpolation
	//assignment    : Any
	//library       : LCon
	//------------------------------------------------------------------------------------------------------------------------
	//change log table:
	//version   date          expert in charge            changes applied
	//V1.0      01.07.2012    APC GMC group               Creation
	//V1.1      17.11.2016    APC ERL                     adapted to styleguide
	//=========================================================================================================================
	
	#LConSMC_Linear := 0.0;
	IF (#x1 <> #x2) THEN
	    IF (#x1 > #x2) THEN
	        #tmpXcalc1 := #x2;
	        #tmpYcalc1 := #y2;
	        #tmpXcalc2 := #x1;
	        #tmpYcalc2 := #y1;
	    ELSE
	        #tmpXcalc1 := #x1;
	        #tmpYcalc1 := #y1;
	        #tmpXcalc2 := #x2;
	        #tmpYcalc2 := #y2;
	    END_IF;
	    IF (#x < #tmpXcalc1) THEN
	        #LConSMC_Linear := #tmpYcalc1;
	    ELSIF (#x > #tmpXcalc2) THEN
	        #LConSMC_Linear := #tmpYcalc2;                                                            // End limits
	    ELSE
	        #LConSMC_Linear := ((#x - #tmpXcalc1) / (#tmpXcalc2 - #tmpXcalc1)) * (#tmpYcalc2 - #tmpYcalc1) + #tmpYcalc1;        // Interpolate between two points
	    END_IF;
	END_IF;
	
	
END_FUNCTION

