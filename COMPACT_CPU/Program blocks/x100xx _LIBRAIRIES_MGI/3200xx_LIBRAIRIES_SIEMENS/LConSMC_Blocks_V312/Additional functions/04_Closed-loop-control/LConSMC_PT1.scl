FUNCTION_BLOCK "LConSMC_PT1"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      set : Bool := FALSE;
      x : LReal := 0.0;
      setvalue : LReal := 0.0;
      smoothingTime : Real := 100.0;
      tCycle : Real;
   END_VAR

   VAR_OUTPUT 
      error : Bool := FALSE;
      errorID : DWord := 0;
      y : LReal;
   END_VAR

   VAR 
      statTmax : LReal;
      statXkm1 : LReal;
      statYkm1 : LReal;
      statFirstRun : Bool := TRUE;
      statOBnumberInternal : Int;
      statY : LReal := 0.0;
   END_VAR

   VAR_TEMP 
      tmpDenom : LReal;
      tmpEnum : LReal;
   END_VAR

   VAR CONSTANT 
      UNIT_ID : DWord := 16#1101_0000;
      MAX_SMOOTHING : Real := 5000.0;
      NO_ERROR : DWord := 16#0000_0000;
   END_VAR


BEGIN
	//------------------------------------------------------------------------------------------------------------------------
	//SIEMENS DF FA PMA APC / Mumbai,India | Erlangen, Germany
	//------------------------------------------------------------------------------------------------------------------------
	//name          : PT1
	//functionality : PT1-Filter
	//assignment    : OB3x (cyclic interupt), OB6x (synchronous operation)
	//library       : LCon
	//------------------------------------------------------------------------------------------------------------------------
	//change log table:
	//version   date          expert in charge            changes applied
	//V1.0      04.07.2012    APC GMC group               Creation
	//V1.1      17.11.2016    APC ERL                     adapted to styleguide; warning "variable might not be initialized" removed
	//V1.2      17.05.2019    APC ERL                     update variables also for smoothingTime <= 0.0 (*c_00015*)
	//========================================================================================================================
	
	#error := FALSE;
	
	IF (#statFirstRun) THEN
	    
	    #statFirstRun := FALSE;                                      // reset marker
	    #statXkm1 := 0.0;                                            // No previous value
	END_IF;
	
	#error := #error OR (#tCycle = 0.0);
	
	IF #error THEN
	    #errorID := #UNIT_ID OR "LCON_ERROR_CYCLE_TIME_ZERO";
	ELSE
	    #errorID := #NO_ERROR;
	END_IF;
	
	IF (NOT #error) THEN
	    
	    IF (#smoothingTime <= 0.0) THEN
	        #statY := #x;
	        #statXkm1 := #x;      // (*c_00015*)
	        #statYkm1 := #statY;  // (*c_00015*)            
	    ELSE
	        #statTmax := REAL_TO_LREAL(2.0 * LIMIT(MN := #tCycle,                                // Tmax=2TS
	                                                                          IN := #smoothingTime,
	                                                                          MX := #MAX_SMOOTHING));
	        IF (#set) THEN               // Check for active SET-command: if active, set Xkm1, y  to SV.                                                     
	            #statY := #setvalue;
	            #statXkm1 := #setvalue;
	        ELSE
	            #tmpEnum := #statTmax - #tCycle;
	            #tmpDenom := #statTmax + #tCycle;
	            #statY := (#tmpEnum * #statYkm1 + #tCycle * (#x + #statXkm1)) / #tmpDenom;
	            #statXkm1 := #x;
	        END_IF;
	        #statYkm1 := #statY;            // previous value same as present
	    END_IF;
	ELSE
	    #statY := 0.0;
	END_IF;
	
	// set outputs
	#y := #statY;
	
END_FUNCTION_BLOCK

